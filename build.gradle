plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.6.0'
	id "com.modrinth.minotaur" version "2.+"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://maven.shedaniel.me" } //rei
	maven { url "https://api.modrinth.com/maven" } //lazyDFU
	maven { url "https://ladysnake.jfrog.io/artifactory/mods" } //CCA
	maven { url "https://maven.terraformersmc.com" } //modmenu, trinkets
	maven { url "https://maven.pinkgoosik.ru" } //cosmetica
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "dev.emi:trinkets:${project.trinkets_version}"

	include modLocalRuntime("ru.pinkgoosik:cosmetica:0.4.0")
	modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:8.0.442"
	modLocalRuntime "com.terraformersmc:modmenu:3.1.0"
	modLocalRuntime "maven.modrinth:lazydfu:0.1.2"
	modLocalRuntime "maven.modrinth:ferrite-core:4.2.0"
}

processResources {
	inputs.property "version", project.mod_version
	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

import com.modrinth.minotaur.dependencies.ModDependency
modrinth {
	projectId = "IsKsViE2"
	versionName = "Artifality " + project.mod_version
	changelog = System.getenv("CHANGELOG")
	versionNumber = project.mod_version
	versionType = "beta"
	uploadFile = remapJar
	additionalFiles = [remapSourcesJar]
	gameVersions = ["1.18.2"]
	dependencies = [
		new ModDependency("P7dR8mSH", "required"), //Fabric API
		new ModDependency("5aaWibi9", "required")  //Trinkets
	]
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url "https://maven.pinkgoosik.ru/"
			credentials {
				username System.getenv("MAVEN_USER")
				password System.getenv("MAVEN_PASS")
			}
		}
	}
}