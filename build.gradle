plugins {
	id 'fabric-loom' version '0.6-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = "mc" + project.minecraft_version + "-" + project.mod_version + "-" + project.release_type
//group = project.maven_group

repositories {

	maven { url "https://maven.terraformersmc.com" }
	maven { url "https://maven.shedaniel.me" }
	mavenCentral()
	maven { url "https://raw.githubusercontent.com/Devan-Kerman/Devan-Repo/master/" }
	maven { url "https://maven.abusedmaster.xyz/" }
	maven { url "https://jitpack.io" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modCompile "dev.emi:trinkets:${project.trinkets_version}"
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:2.8.2"
	modImplementation "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:2.8.2"
	
	modImplementation "com.oroarmor:multi-item-lib:1.3.1"
	include "com.oroarmor:multi-item-lib:1.3.1"
	
	modImplementation group: 'net.devtech', name: 'arrp', version: '0.3.11'
	include group: 'net.devtech', name: 'arrp', version: '0.3.11'
	
	modRuntime "me.shedaniel:RoughlyEnoughItems:5.9.170"
	modRuntime "com.terraformersmc:modmenu:1.16.6"

}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
	// JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
	// We'll use that if it's available, but otherwise we'll use the older option.
	def targetVersion = 8
	if (JavaVersion.current().isJava9Compatible()) {
		 it.options.release = targetVersion
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
