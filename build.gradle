plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower-mini' version '1.2.1'
	id 'com.modrinth.minotaur' version '1.2.1'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "-" + project.minecraft_version + "-fabric"
group = project.maven_group

repositories {
	maven { url "https://maven.shedaniel.me" } //rei
	maven { url "https://maven.gegy.dev" } //databreaker
	maven { url "https://ladysnake.jfrog.io/artifactory/mods" } //CCA
	maven { url "https://maven.terraformersmc.com" } //modmenu, trinkets
	maven { url "https://maven.pinkgoosik.ru" } //cosmetica
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "dev.emi:trinkets:${project.trinkets_version}"
	
	include modImplementation("ru.pinkgoosik:cosmetica:0.3.2-1.17.1-fabric")
	include modImplementation("ru.pinkgoosik:goosik-config:0.2.0-1.17.1-fabric")
	
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:7.1.357"
	modRuntimeOnly "com.terraformersmc:modmenu:3.0.1"
	modRuntimeOnly "supercoder79:databreaker:0.2.8"
}

processResources {
	inputs.property "version", project.mod_version
	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			url "https://maven.pinkgoosik.ru/"
			credentials {
				username System.getenv("MAVEN_REPO_USER")
				password System.getenv("MAVEN_REPO_PASS")
			}
		}
	}
}

curseforge {
	apiKey System.getenv('CURSEFORGE_TOKEN') ?: ''

	project {
		id = '490812'
		releaseType = 'beta'
		changelog = project.mod_changelog
		addGameVersion '1.18.1'
		addGameVersion "Fabric"
		mainArtifact(remapJar) { displayName = "[${project.minecraft_version}] Artifality ${project.mod_version}" }
		afterEvaluate {
			uploadTask.dependsOn("remapJar")
		}
	}
	options {
		forgeGradleIntegration = false
	}
}

import com.modrinth.minotaur.TaskModrinthUpload
import com.modrinth.minotaur.request.VersionType
task modrinth(type: TaskModrinthUpload) {
	token = System.getenv('MODRINTH_TOKEN') ?: ''
	onlyIf { !token.isEmpty() }

	projectId = 'IsKsViE2'
	versionName = "[${project.minecraft_version}] Artifality ${project.mod_version}"
	versionType = VersionType.BETA
	versionNumber = project.version
	changelog = project.mod_changelog
	addLoader 'fabric'
	addGameVersion '1.18.1'
	uploadFile = remapJar
}

publish.finalizedBy tasks.curseforge, tasks.modrinth
